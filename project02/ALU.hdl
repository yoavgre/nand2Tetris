// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    
    Mux16(a=x, b[0..15]=false, sel=zx, out=outzx);
    Not16(in=outzx, out=notoutzx); 
    Mux16(a=outzx, b=notoutzx, sel=nx, out=x1);
    Mux16(a=y, b[0..15]=false, sel=zy, out=outzy);
    Not16(in=outzy, out=notoutzy); 
    Mux16(a=outzy, b=notoutzy, sel=ny, out=y1);
    Add16(a=x1, b=y1, out=x1Plusy1);
    And16(a=x1, b=y1, out=x1Andy1);
    Mux16(a=x1Andy1, b=x1Plusy1, sel=f, out=out1);
    Not16(in=out1, out=nout1);
    Mux16(a=out1, b=nout1, sel=no, out=out, out=out2);
    Or8Way(in=out2[0..7], out=or07);
    Or8Way(in=out2[8..15], out=or815);
    Or(a=or07, b=or815, out=zr1);
    Not(in=zr1, out=zr);
    Or16(a=out2, b[0..15]=false, out[15]=ng);

    
}