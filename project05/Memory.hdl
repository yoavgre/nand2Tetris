// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    DMux4Way(in=load, sel=address[13..14], a=ramIn1, b=ramIn2, c=screenInput, d=null);//choosing if the input is for the ram16k or the screen
    Or(a=ramIn1, b=ramIn2, out=ramInput); // this two options goes to the ram16k memory

    RAM16K(in=in, load=ramInput, address=address[0..13], out=ramOut); // loading the ram16k from the input based on the address
    Screen(in=in, load=screenInput, address=address[0..12], out=scOut); // loading the screen from the input
    Keyboard(out=kbout); 

    Mux4Way16(a=ramOut, b=ramOut, c=scOut, d=kbout, sel=address[13..14], out=out); //chooses the output based on the address last digits
}
